// This Closure is applied to each subproject of this (parent) project
subprojects {
    apply plugin: 'java'

    group = 'com.jimtough'
    version = '1.0-SNAPSHOT'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        mavenCentral()
    }

}

//--------------------------------------------------------------------------------------
// Get the Project object for a specific subproject, and use a Closure to act upon it.
// NOTE: A colon before the subproject name is required.
project(':picker-number-app-1') {
    dependencies {
        implementation 'org.apache.commons:commons-math3:3.2'
        implementation 'org.apache.logging.log4j:log4j-core:2.5'
        testImplementation  "junit:junit:4.12"
    }
}

project(':plus-plus') {
    dependencies {
        // Declaring a dependency on another project of the same multi-project build
        implementation project(':random-number-gen-1')
    }
}

project(':random-number-gen-1') {

    apply plugin: 'java-library'

    dependencies {
        // 'api' scope cannot be used with bare 'java' plugin - requires 'java-library' plugin.
        // We use 'api' scope because any other project that uses this project must expose this library's API.
        api 'org.apache.commons:commons-math3:3.2'
        implementation 'org.apache.logging.log4j:log4j-core:2.5'
        testImplementation "junit:junit:4.12"
    }
}
//--------------------------------------------------------------------------------------

